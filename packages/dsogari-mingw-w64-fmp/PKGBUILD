# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=fmp
pkgbase=dsogari-mingw-w64-${_realname}
pkgname="dsogari-${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.11
pkgrel=1
pkgdesc="FalkerMap2 core library (mingw-w64)"
arch=('any')
url="http://www.falker.com.br/produto-falkermap-software-agricultura-precisao.php"
license=('Proprietary')
makedepends=("git"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-ninja")
checkdepends=("dsogari-${MINGW_PACKAGE_PREFIX}-googletest")
depends=("${MINGW_PACKAGE_PREFIX}-armadillo"
         "${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-breakpad"
         "${MINGW_PACKAGE_PREFIX}-ceres-solver"
         "${MINGW_PACKAGE_PREFIX}-libarchive"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-wkhtmltopdf-git"
         "dsogari-${MINGW_PACKAGE_PREFIX}-cpporm"
         "dsogari-${MINGW_PACKAGE_PREFIX}-gdal"
         "dsogari-${MINGW_PACKAGE_PREFIX}-polymul")
source=("${_realname}-${pkgver}.tar.gz")
sha256sums=('0dc9e8b0b0315409c5f155c9a970d8752f9602bad6a7ca2863c3ccf60d3b7581')

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  mkdir -p data/FalkerMap/locale/en_US/LC_MESSAGES
  mkdir -p data/FalkerMap/locale/pt_BR/LC_MESSAGES

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTING=OFF \
    -DFMP_ENABLE_PCH=OFF \
    "../${_realname}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
