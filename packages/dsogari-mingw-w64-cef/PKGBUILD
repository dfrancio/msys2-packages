# Maintainer: Diego Sogari <diego.sogari@gmail.com>

# detect architecture
if [[ ${MINGW_CHOST} == *"i686"* ]]; then
  _dir="cef_binary_3.3029.1611.g44e39a8_windows32_minimal"
  _hash="cb82efc68d5ac5eadb1645bbd206caa2503710cfc53d5c22fbfe0b4e529511f5"
else
  _dir="cef_binary_3.3029.1611.g44e39a8_windows64_minimal"
  _hash="70fc85028d1a361f45f3d2a0023e517528bb57638f7a6feba7aa05c780215ae2"
fi

_realname=cef
pkgbase=dsogari-mingw-w64-${_realname}
pkgname="dsogari-${MINGW_PACKAGE_PREFIX}-${_realname}"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.3029.1611.g44e39a8
pkgrel=1
pkgdesc="Chromium Embedded Framework (mingw-w64)"
arch=('any')
url="https://bitbucket.org/chromiumembedded/cef"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
source=("http://opensource.spotify.com/cefbuilds/${_dir}.tar.bz2"
        "${_realname}-3.3029.1611.g44e39a8.patch")
sha256sums=("${_hash}"
            '61798a8291bd57750e50050b6319ec58b41f32f9f6519c455460f4b043bbb948')

prepare() {
  cd "${srcdir}/${_dir}"
  patch --binary -Np1 -i "../${_realname}-3.3029.1611.g44e39a8.patch"
  sed -e '/add_subdirectory(tests/d' -i CMakeLists.txt
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    "../${_dir}"

  make
}

package() {
  mkdir -p "${pkgdir}${MINGW_PREFIX}/opt"
  mv "${srcdir}/${_dir}" "${pkgdir}${MINGW_PREFIX}/opt/cef"
}
