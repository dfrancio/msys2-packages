# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=cef
pkgbase=dsogari-mingw-w64-${_realname}
pkgname="dsogari-${MINGW_PACKAGE_PREFIX}-${_realname}"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.2924.1575.g97389a9
pkgrel=1
pkgdesc="Chromium Embedded Framework (mingw-w64)"
arch=('any')
url="https://bitbucket.org/chromiumembedded/cef"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
declare -A dirs=(\
  ["i686-w64-mingw32"]="cef_binary_${pkgver}_windows32"\
  ["x86_64-w64-mingw32"]="cef_binary_${pkgver}_windows64"\
)
declare -A hashes=(\
  ["i686-w64-mingw32"]="c84f7eb44c705823fd6bf50538bcc9dcb5fcfb85636c0248b2c9d222be3eeec7"\
  ["x86_64-w64-mingw32"]="a84e1edbdf8dec7b7acc17c6044e10dba14a292f45636d3f32533eb7263ef708"\
)
source=("http://opensource.spotify.com/cefbuilds/${dirs[${MINGW_CHOST}]}.tar.bz2"
        "${_realname}-3.2924.1575.g97389a9.patch")
sha256sums=("${hashes[${MINGW_CHOST}]}"
            '7da094d017295dc9110bed3b60138f97547a0c166e1ca7e35f0b36ea2d94bb18')

prepare() {
  cd "${srcdir}/${dirs[${MINGW_CHOST}]}"
  patch --binary -Np1 -i "../${_realname}-3.2924.1575.g97389a9.patch"
}

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    "../${dirs[${MINGW_CHOST}]}"

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
