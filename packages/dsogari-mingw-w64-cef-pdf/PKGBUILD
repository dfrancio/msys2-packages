# Maintainer: Diego Sogari <diego.sogari@gmail.com>

_realname=cef-pdf
pkgbase=dsogari-mingw-w64-${_realname}
pkgname="dsogari-${MINGW_PACKAGE_PREFIX}-${_realname}"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.3.0
pkgrel=3
pkgdesc="HTML to PDF utility based on CEF (mingw-w64)"
arch=('any')
url="https://github.com/dsogari/cef-pdf"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "dsogari-${MINGW_PACKAGE_PREFIX}-cef")
depends=("${MINGW_PACKAGE_PREFIX}-boost")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/dsogari/cef-pdf/archive/v${pkgver}.tar.gz")
sha256sums=('ff1b8ee61d574c09ce3774894e92fd5eadf33d87d3c2225ebdeb32cacebc756c')

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCEF_ROOT=$(cygpath -m ${MINGW_PREFIX}/opt/cef) \
    "../${_realname}-${pkgver}"

  make
}

package() {
  mkdir -p "${pkgdir}${MINGW_PREFIX}/opt"
  mv "build-${MINGW_CHOST}/src/Release" "${pkgdir}${MINGW_PREFIX}/opt/cef-pdf"
}
